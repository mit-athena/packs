# Prototype file for setting up course-specific environment
# $Author: epeisach $
# $Source: /afs/dev.mit.edu/source/repository/packs/dotfiles/env_setup,v $
# $Header: /afs/dev.mit.edu/source/repository/packs/dotfiles/env_setup,v 1.2 1989-08-18 14:59:05 epeisach Exp $

alias extend 'if (-d \!:2) if ("$\!:1" \!~ *"\!:2"*) setenv \!:1 ${\!:1}:\!:2'

set dir = `/bin/athena/attach -p $SUBJECT`
if (! $status) then
	set prompt = "$SUBJECT% "
	if ( -r $dir/.attachrc) then
		source $dir/.attachrc
        else
		extend PATH $dir/${hosttype}bin
		extend MANPATH $dir/man
        endif  
else
	echo ""
        echo "The $SUBJECT filesystem could not be attached."
	set filsys = `hesinfo $SUBJECT filsys`
	if ($#filsys < 3) then
		echo "Check your spelling of the filesystem name."
	else
		echo "The $filsys[1] fileserver named $filsys[3] may be down."
	endif
	echo ""
        echo "Type exit to get rid of this window."   
endif
unset dir
unsetenv SUBJECT	# so that child shells won't do all this again.
